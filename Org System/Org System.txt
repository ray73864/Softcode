@sql CREATE TABLE orgsys_orglist (ID INT NOT NULL AUTO_INCREMENT,orgtype VARCHAR(10) NOT NULL,parentorg INT,displayname VARCHAR(50), status VARCHAR(10) NOT NULL, shortname VARCHAR(20), name VARCHAR(50) NOT NULL, description TEXT, PRIMARY KEY (id))
@sql CREATE TABLE orgsys_membership (ID INT NOT NULL AUTO_INCREMENT, playerid VARCHAR(18) NOT NULL, org INT NOT NULL, status VARCHAR(10) NOT NULL, rank INT, position VARCHAR(100), PRIMARY KEY (ID))
@sql CREATE TABLE orgsys_ranks (ID INT NOT NULL AUTO_INCREMENT, org INT NOT NULL, rank INT NOT NULL, male VARCHAR(50), female VARCHAR(50), PRIMARY KEY (ID))
@sql CREATE TABLE orgsys_managers (ID INT NOT NULL AUTO_INCREMENT, orgid INT NOT NULL, playerID VARCHAR(18) NOT NULL, PRIMARY KEY (ID))
@sql CREATE TABLE orgsys_spaceobjects (ID INT NOT NULL AUTO_INCREMENT, orgid INT NOT NULL, objectID VARCHAR(18) NOT NULL, objecttype VARCHAR(10) NOT NULL, PRIMARY KEY (ID))
@sql CREATE TABLE orgsys_races (ID INT NOT NULL AUTO_INCREMENT, orgid INT NOT NULL, race VARCHAR(18) NOT NULL, PRIMARY KEY (ID)) 

@create $Master Room: Org Commands
@set $Master Room: Org Commands = WIZARD
@set $Master Room: Org Commands = !NO_COMMAND
&CMD $Master Room: Org Commands=
&CMD`CLEANUP $Master Room: Org Commands=$^org cleanup(?\: (\w.+))?$:th [if(u(lock.admin),if(%1,org(localcleanup,%1),org(globalcleanup)))]
&CMD`DEMOTE $Master Room: Org Commands=$demote *:th [if(u(lock.manager),[if(strmatch(%0,*=*),[setq(p,toolkit(org,getchar,before(%0,=)),s,toolkit(org,orgnum,after(%0,=)))][switch([t(%qp)][t(%qs)],11,if(match(if(orflags(%#,Wrk),toolkit(org,orgs,%qp),setinter(toolkit(org,orgs,%:),toolkit(org,orgs,%qp))),%qs),u(%!/fun`demote,%:,%qp,%qs),u(%!/fun`msg,%#,You are not in the same suborg.)),0?,u(%!/fun`msg,%#,That character could not be found.),?0,u(%!/fun`msg,%#,That is not a valid suborg.))],[setq(p,toolkit(org,getchar,%0))][if(%qp,[setq(s,setinter(toolkit(org,orgs,%:),toolkit(org,orgs,%qp)))][switch(words(%qs),0,u(%!/fun`msg,%#,You are not in any of the same suborgs as [name(%qp)].),1,u(%!/fun`demote,%:,%qp,%qs),>1,u(%!/fun`msg,%#,You are in multiple suborgs with [name(%qp)].  Please specify with [ansi(hy,demote %<name%>=%<suborg%>)].))],u(%!/fun`msg,%#,That character could not be found.))])])]
&CMD`@EMPIRES $Master Room: Org Commands=$@emp*:th [u(fun`@empires)]
&CMD`ORG.DESC $Master Room: Org Commands=$org desc* *:th [if(u(lock.admin),[if(strmatch(%1,*=*),[u(fun`org.desc,before(%1,=),after(%1,=))],u(fun`msg,%#,{Correct syntax is [ansi(hy,org desc %<org%>=%<description%>)].}))])]
&CMD`ORG.DETACH $Master Room: Org Commands=$org detach*:th [if(u(lock.royal),[if(strmatch(%0,%b*),[u(fun`org.detach1,after(%0,%b))],u(fun`msg,%#,{Correct syntax is [ansi(hy,org detach %<character%>)].}))])]
&CMD`ORG.DETACHSPACE $Master Room: Org Commands=$^org spacedetach (\w.+)$:@nspemit %#=[if(u(lock.royal),if(setr(n,objid(spacenum(%1*))),if(not(setr(s,sql(select COUNT(ID) from orgsys_spaceobjects where orgid='%qM' and objectid='%qN'))),ansi(r,Space-object already not a member of any organisation),[sql({delete from orgsys_spaceobjects where objectid='%qN'})][ansi(c,[ansi(hy,name(%qN))] detached.)]),[ansi(r,Invalid space-object)]))]
@set $Master Room: Org Commands/CMD`ORG.DETACHSPACE=regexp
&CMD`ORG.DISPLAYNAME $Master Room: Org Commands=$org disp* *:th [if(u(lock.admin),[if(strmatch(%1,*=*),[u(fun`org.displayname,before(%1,=),after(%1,=))],u(fun`msg,%#,{Correct syntax is [ansi(hy,org displayname %<org%>=%<ansi text%>)].}))])]
&CMD`ORG.JOIN $Master Room: Org Commands=$org join *:th [if(u(lock.admin),[if(strmatch(%0,*=*),[setq(p,toolkit(org,getchar,before(%0,=)))][setq(o,toolkit(org,orgnum,after(%0,=)))][switch([t(%qp)][t(%qo)],11,[if(match(sql(select orgtype from orgsys_orglist where id=%qo),MO),if(toolkit(org,morg,%qp),[u(fun`msg,%#,{[name(%qp)] already belongs to a masterorg.})],[u(fun`msg,%#,{[name(%qp)] added to [toolkit(org,orgname,%qo)].})][u(fun`msg,%qp,{You've been added to [toolkit(org,orgname,%qo)].})][u(fun`org.join,%qp,%qo)]),[switch([t(match([toolkit(org,orgs,%qp)],%qo))][t(toolkit(org,morg,%qp))],01,[u(fun`msg,%#,{[name(%qp)] joined to [toolkit(org,orgname,%qo)].})][u(fun`msg,%qp,{You have been added to [toolkit(org,orgname,%qo)].})][u(fun`org.join,%qp,%qo,[not(t(ulocal(#5824/fun.org`mainsub,%qp)))])],1?,[u(fun`msg,%#,{[name(%qp)] already belongs to [toolkit(org,orgname,%qo)].})],?0,[u(fun`msg,%#,{[name(%qp)] does not have a masterorg set.})])])],0?,[u(fun`msg,%#,That character could not be found.)],?0,[u(fun`msg,%#,What suborg do you mean?)])],u(fun`msg,%#,Proper syntax is [ansi(hy,org join %<player%>=%<org%>)].))])]
&CMD`ORG.ATTACH $Master Room: Org Commands=$^org attach/(ship|base|planet) (\w.+)=(\w.+)$:@nspemit %#=[if(u(lock.royal),if(setr(n,objid(spacenum(%2*))),if(setr(m,toolkit(org,orgnum,%3)),if(setr(s,sql(select COUNT(ID) from orgsys_spaceobjects where orgid='%qM' and objectid='%qN')),ansi(r,Space-object already a member of that organisation),[sql({insert into orgsys_spaceobjects (orgid,objectid,objecttype) values (%qM,'%qN','%1')})][ansi(c,[ansi(hy,name(%qN))] \([sql({select last_insert_id()})]\) attached to organisation [ansi(hy,toolkit(org,orgname,%qM))] \(%qM\).)]),[ansi(r,Invalid organisation)]),[ansi(r,Invalid space-object)]))]
&CMD`ORG.LIST $Master Room: Org Commands=$org list:th [if(setr(o,toolkit(org,morg,%:)),[nspemit(%#,[header(,Organizational Listing,[toolkit(org,orgname,%qo)])]%R%R[u(fun`suborglist,%qo)]%R%R[header(,%?,[u(%!/version)])])],[u(fun`@empires)])]
&CMD`ORG.LIST.2 $Master Room: Org Commands=$org list *:th [if(u(lock.admin),[if(setr(o,toolkit(org,orgnum,[secure(%0)])),[nspemit(%#,[u(fun_header,Organizational Listing,[toolkit(org,orgname,%qo)])]%R%R[u(fun`suborglist,%qo)]%R%R[u(fun_footer)])],[u(fun`msg,%#,That is not a valid org.)])]
&CMD`ORG.NEWMO $Master Room: Org Commands=$org newmo*:th [if(u(lock.wizard),[if(strmatch(%0,*=*),[u(fun`org.newmo,squish(before(%0,=)),after(%0,=))],u(fun`msg,%#,{Correct syntax is [ansi(hy,org newmo %<name%>=%<abbreviation%>)].}))])]
&CMD`ORG.NEWSO $Master Room: Org Commands=$org newso *=*:th [if(u(lock.admin),[if(setr(0,toolkit(org,orgnum,[secure(%1)])),[u(fun`org.newso,%0,%q0)])]
&CMD`ORG.PLAYERS $Master Room: Org Commands=$org players:th [if(setr(o,toolkit(org,morg,%:)),[u(fun`org.players,%qo)],[u(fun`msg,%#,You do not belong to a masterorg.)])]
&CMD`ORG.PLAYERS.2 $Master Room: Org Commands=$org players *:th [if(u(lock.admin),[if(setr(o,toolkit(org,orgnum,%0)),[u(fun`org.players,%qo)],[u(fun`msg,%#,That is not a valid org.)])])]
&CMD`ORG.RANKS $Master Room: Org Commands=$org rank */*=*/*:th [if(u(lock.manager),[if(setr(o,toolkit(org,orgnum,%0)),u(fun`org.ranks,%qo,%1,%2,%3))]
&CMD`ORG.RANK $Master Room: Org Commands=$org rank *=*:th [if(u(lock.manager),[if(setr(o,toolkit(org,orgnum,%0)),[sql(UPDATE orgsys_membership SET rank=],ansi(r,Invalid organisation))],ansi(r,You must be a manager to use this command.))]
&CMD`ORG.REMOVE $Master Room: Org Commands=$org rem* *:th [if(u(lock.admin),[if(strmatch(%1,*=*),[u(fun`org.remove1,before(%1,=),after(%1,=))],u(fun`msg,%#,{Correct syntax is [ansi(hy,org remove %<player%>=%<org%>)].}))])]
&CMD`ORG.RENAME $Master Room: Org Commands=$org rename *:th [if(u(lock.admin),[if(strmatch(%0,*=*),[u(fun`org.rename,before(%0,=),after(%0,=))],u(fun`msg,%#,{Correct syntax is [ansi(hy,org displayname %<org%>=%<ansi text%>)].}))])]
&CMD`ORG.STATUS $Master Room: Org Commands=$org stat* *:th [if(u(lock.admin),[if(strmatch(%1,*=*),[u(fun`org.status,before(%1,=),after(%1,=))],u(fun`msg,%#,{Correct syntax is [ansi(hy,org status %<org%>=%<OPEN|CLOSED|OOC|NPC%>)].}))])]
&CMD`SPACE-OBJECT.UNKNOWN $Master Room: Org Commands=$^org/unknown (ships|bases)$:@assert isstaff(%#)={@nspemit %#=ansi(r,You are not allowed to do that.)};@nspemit %#=u(fun`unknown,%1)
@set $Master Room: Org Commands/CMD`SPACE-OBJECT.UNKNOWN=regexp
&CMD`ORG.TRANSFER $Master Room: Org Commands=$org trans* *:th [if(u(lock.admin),[if(strmatch(%1,*=*),[u(fun`org.transfer,before(%1,=),after(%1,=))],u(fun`msg,%#,{Correct syntax is [ansi(hy,org transfer %<character%>=%<org%>)].}))])]
&CMD`POSITION $Master Room: Org Commands=$^position (.+)=(.+)$:@assert [u(lock.manager,%1)]={@nspemit %#=[u(fun`msg.unauthorized,%#)]};@assert t(setr(P,getplayer(%1)))={u(%!/fun`msg,%#,That character could not be found.)};th u(%!/fun`position,%:,%qp,sorg(%1),%2)
@set $Master Room: Org Commands/CMD`POSITION=regexp
&CMD`PROMOTE $Master Room: Org Commands=$promote *:th [if(u(lock.manager,%0),[if(strmatch(%0,*=*),[setq(p,toolkit(org,getchar,before(%0,=)),s,toolkit(org,orgnum,after(%0,=)))][switch([t(%qp)][t(%qs)],11,if(match(if(orflags(%#,Wrk),toolkit(org,orgs,%qp),setinter(toolkit(org,orgs,%:),toolkit(org,orgs,%qp))),%qs),u(%!/fun`promote,%:,%qp,%qs),u(%!/fun`msg,%#,You are not in the same suborg.)),0?,u(%!/fun`msg,%#,That character could not be found.),?0,u(%!/fun`msg,%#,That is not a valid suborg.))],[setq(p,toolkit(org,getchar,%0))][if(%qp,[setq(s,setinter(toolkit(org,orgs,%:),toolkit(org,orgs,%qp)))][switch(words(%qs),0,u(%!/fun`msg,%#,You are not in any of the same suborgs as [name(%qp)].),1,u(%!/fun`promote,%:,%qp,%qs),>1,u(%!/fun`msg,%#,You are in multiple suborgs with [name(%qp)].  Please specify with [ansi(hy,promote %<name%>=%<suborg%>)].))],u(%!/fun`msg,%#,That character could not be found.))])])]

&CMD`ORG`ADDRACE $Master Room: Org Commands=$^org/race/(add|delete) (\w+)=(\w+)$:@break isstaff(%#)={@break t(isdbref(%3))={@break t(setr(O,toolkit(org,orgnum,%2)))={@assert t(org(hasrace,%qO,%3))={@sql INSERT INTO orgsys_races (orgid,race) VALUES ('%qO','[sqlescape(%3)]');@nspemit %#=ansi(c,You have added [ansi(hy,name(%3))] to the [ansi(hy,org(orgname,%qO))] race list)};@nspemit %#=ansi(r,[name(%3)] is already on the [toolkit(org,orgname,%qO)] race list)};@nspemit %#=ansi(r,Invalid Organisation)};@nspemit %#=ansi(r,Invalid Race Dbref#)};@nspemit %#=ansi(r,Permission Denied)
@set $-T:Master Room: MagSystem SQL/CMD`ORG`ADDRACE=regexp

@DESCRIBE $Master Room: Org Commands=Commands for the OrgSys go here.
@set $Master Room: Org Commands/DESCRIBE=no_command visual prefixmatch public nearby
&FUN $Master Room: Org Commands=
&FUN`ALLSUBORGS $Master Room: Org Commands=[setq(a,setdiff(sql(select id from orgsys_orglist where orgtype='SO' and parentorg=%0),%qs))][if(%qa,[setq(s,setunion(%qs,%qa))][iter(%qa,u(fun`allsuborgs,[itext(0)]))])]
&FUN`DEMOTE $Master Room: Org Commands=[setq(0,u(#5824/fun.org`ranknum,%0,%2),1,u(#5824/fun.org`ranknum,%1,%2))][if(cor(orflags(%0,Wrk),gte(sub(%q0,%q1),1)),[if(isint(sql(select rank from orgsys_ranks where org='%2' and rank='[sub(%q1,1)]')),[sql(update orgsys_membership set rank='[sub(%q1,1)]' where org='%2' and playerid='%1')][u(%!/fun`msg,%0,You demote [name(%1)] to rank [u(#5824/fun.org`rank,%1,%2)].)][u(%!/fun`msg,%1,[name(%0)] demotes you to rank [u(#5824/fun.org`rank,%1,%2)].)],u(%!/fun`msg,%0,There is not a lower rank to demote to.))],u(%!/fun`msg,%0,You can't demote that character.))]
&FUN`@EMPIRES $Master Room: Org Commands=[setq(0,squish([setq(l,sql({select id, displayname from orgsys_orglist where orgtype='mo'},~,|))][iter(%ql,[setq(s,[before(##,|)])][squish([u(fun`allsuborgs,[before(##,|)])])][setq(c,)][iter(%qs,[setq(c,setunion(%qc,sql(select playerid from orgsys_membership where org=[itext(0)])))])][after(##,|)]-[words(%qc)]-[sql(select status from orgsys_orglist where id=[before(##,|)])]-[sql(select shortname from orgsys_orglist where id=[before(##,|)])],~,~)]))][nspemit(%#,[u(fun_header,org list)]%R[ansi(hR,[align(>7 <30 -9 <7,Alias,%bOrganization,Members,Status)])]%R[iter(%q0,[align(>7 <30 -9 <7,[elements(##,4,-)],%b[elements(##,1,-)],[elements(##,2,-)],[switch([elements(##,3,-)],OPEN,[ansi(hg,OPEN)],OOC,[ansi(r,OOC)],NPC,[ansi(g,NPC)],[ansi(hr,CLOSED)])])],~,%R)]%R[u(fun_footer)]%R)]
&FUN_FOOTER $Master Room: Org Commands=%r[header(%?,name(%#),ansi(hw,ictime(%#)),sub(width(%#),2),Gx)]
&FUN_HEADER $Master Room: Org Commands=[header(ansi(hw,OrgSystem),%0,%1,sub(width(%#),2),Gx)]%r
&FUN`MSG $Master Room: Org Commands=[nspemit(%0,prefix({%1},,OrgSys))]
&FUN`MSG.UNAUTHORIZED $Master Room: Org Commands=[nspemit(%0,prefix([ansi(hr,You are not authorized to do that.)],,OrgSys))]
&FUN`ORG.DESC $Master Room: Org Commands=[if(setr(o,toolkit(org,orgnum,%0)),[sql(update orgsys_orglist set description='[sqlescape(%1)]' where id=%qo)][u(fun`msg,%#,{Description for [toolkit(org,orgname,%qo)] set.})],[u(fun`msg,%#,That is not a valid org.)])]
&FUN`ORG.DETACH $Master Room: Org Commands=sql({delete from orgsys_membership where playerid='%0'})
&FUN`ORG.DETACH1 $Master Room: Org Commands=[if(setr(p,toolkit(org,getchar,%0)),[if(or(toolkit(org,orgs,%qp),toolkit(org,morg,%qp)),[u(fun`msg,%#,{[name(%qp)] has been detached and removed from all orgs and suborgs.})][u(fun`msg,%qp,{You've been removed from all orgs and suborgs.})][u(fun`org.detach,%qp)],[u(fun`msg,%#,{[name(%qp)] doesn't belong to any orgs or suborgs.})])],[u(fun`msg,%#,Who did you want to detach?)]
&FUN`ORG.DISPLAYNAME $Master Room: Org Commands=[if(setr(o,toolkit(org,orgnum,%0)),[sql(update orgsys_orglist set displayname='[sqlescape(%1)]' where id=%qo)][u(fun`msg,%#,{Display name for [toolkit(org,orgname,%qo)] set to [sql(select displayname from orgsys_orglist where id=%qo)].})],[u(fun`msg,%#,That is not a valid org.)])]
&FUN`ORG.JOIN $Master Room: Org Commands=if(%2,sql({insert into orgsys_membership (playerid, org, status) values ('%0', %1, 'MAIN')}),sql({insert into orgsys_membership (playerid, org) values ('%0', %1)}))
&FUN`ORG.NEWMO $Master Room: Org Commands=[if(or(toolkit(org,orgnum,%0),toolkit(org,orgnum,%1)),[u(fun`msg,%#,That's an invalid org name or alias.)],[sql({insert into orgsys_orglist (name, shortname, displayname, orgtype) values ('[sqlescape(%0)]', '[sqlescape(%1)]', '[sqlescape(%0)]', 'MO')})][if(setr(o,toolkit(org,orgnum,%1)),[u(fun`msg,%#,{Masterorg [toolkit(org,orgname,%qo)] created with alias '%1'.})],[u(fun`msg,%#,Masterorg not created.)])])]
&FUN`ORG.NEWSO $Master Room: Org Commands=[if(not(sql(SELECT name FROM orgsys_orglist where name='%0')),sql(INSERT INTO orgsys_orglist (name,parentorg,orgtype) VALUES ('[sqlescape(%0)]',%1,SO))[nspemit(%#,u(fun`msg,%#,{New Suborg created \(%0\) and attached to org \(%1\)}))])]
&FUN`ORG.PLAYERS $Master Room: Org Commands=[nspemit(%#,{[header(,Players Listing,u(#5824/fun.org`orgdname,%0))]%R%R[ansi(hG,[align(>15 15 25 20,Rank, Name, Position, Organization,,[ansi(X,%b)])])]%R[iter(u(#5824/fun.org`players,%0),[setq(x,[firstof(first(setinter(toolkit(org,suborgs,%0),u(#5824/fun.org`orgs,##))),%0)])][align(>15 15 25 20,u(#5824/fun.org`rank,##,%qx),[name(##)],u(#5824/fun.org`position,##,%qx),[sql(select displayname from orgsys_orglist where id=%qx)])],,%R)]%R%R[header(,%?,ictime())]%R})]
&FUN`ORG.RANKS $Master Room: Org Commands=[if(sql(SELECT rank FROM orgsys_ranks WHERE rank='%1'),sql(UPDATE orgsys_ranks SET male='[sqlescape(%2)]', female='[sqlescape(%3)]' WHERE rank='%1')[nspemit(%#,prefix(Rank %1: Male \(%2\)\, Female \(%3\) Updated for %0,Gx,OrgSys))],sql(INSERT INTO orgsys_ranks (org,rank,male,female) VALUES (%0,%1,'[sqlescape(%2)]','[sqlescape(%3)]'))[nspemit(%#,prefix(New Rank %1: Male \(%2\)\, Female \(%3\) for %0 created,Gx,OrgSys))])
&FUN`ORG.REMOVE $Master Room: Org Commands=sql({delete from orgsys_membership where playerid='%0' and org=%1})
&FUN`ORG.REMOVE1 $Master Room: Org Commands=[setq(p,toolkit(org,getchar,%0))][setq(o,toolkit(org,orgnum,%1))][switch([t(%qp)][t(%qo)],11,[if(match(sql(select orgtype from orgsys_orglist where id=%qo),MO),[switch([t(match(toolkit(org,morg,%qp),%qo))][t(toolkit(org,orgs,%qp))],10,[u(fun`msg,%#,{[name(%qp)] removed from [toolkit(org,orgname,%qo)].})][u(fun`msg,%qp,{You have been removed from [toolkit(org,orgname,%qo)].})][u(fun`org.remove,%qp,%qo)],0?,[u(fun`msg,%#,{[name(%qp)] doesn't belong to that masterorg.})],?1,[u(fun`msg,%#,{Remove [name(%qp)]'s suborgs or use 'org detach' first.})])],[switch([t(match([toolkit(org,orgs,%qp)],%qo))][t(toolkit(org,morg,%qp))],11,[u(fun`msg,%#,{[name(%qp)] removed from [toolkit(org,orgname,%qo)].})][u(fun`msg,%qp,{You have been removed from [toolkit(org,orgname,%qo)].})][u(fun`org.remove,%qp,%qo)],0?,[u(fun`msg,%#,{[name(%qp)] doesn't belong to [toolkit(org,orgname,%qo)].})],?0,[u(fun`msg,%#,{[name(%qp)] does not have a masterorg set.})])])],0?,[u(fun`msg,%#,That character could not be found.)],?0,[u(fun`msg,%#,What suborg did you mean?)])]
&FUN`ORG.RENAME $Master Room: Org Commands=[if(setr(o,toolkit(org,orgnum,%0)),[sql(update orgsys_orglist set name='[sqlescape(%1)]' where id=%qo)][u(fun`msg,%#,{Display name for [toolkit(org,orgname,%qo)] set to %1.})],[u(fun`msg,%#,That is not a valid org.)])]
&FUN`ORG.STATUS $Master Room: Org Commands=[setq(o,toolkit(org,orgnum,secure(%0)))][setq(s,switch(secure(%1),op*,OPEN,cl*,CLOSED,oo*,OOC,np*,NPC,#-1 STATUS MUST BE OPEN CLOSED OOC NPC))][switch([t(%qo)][t(%qs)],11,[sql(update orgsys_orglist set status='%qs' where id=%qo)][u(fun`msg,%#,{[toolkit(org,orgname,%qo)] is now set to %qs.})],0?,[u(fun`msg,%#,That is not a valid org.)],?0,[u(fun`msg,%#,{Status types can be OPEN, CLOSED, OOC, or NPC.})])]
&FUN`ORG.TRANSFER $Master Room: Org Commands=[setq(p,toolkit(org,getchar,%0))][setq(o,toolkit(org,orgnum,%1))][setq(m,toolkit(org,morg,%qp))][switch([t(%qp)][t(%qo)][t(match(%qm,%qo))],110,[switch([sql(select orgtype from orgsys_orglist where id=%qo)],MO,[u(fun`org.remove,%qp,%qm)][u(fun`org.join,%qp,%qo)][u(fun`msg,%#,{[name(%qp)] has been transferred to [toolkit(org,orgname,%qo)].})][u(fun`msg,%qp,{You have been transferred to [toolkit(org,orgname,%qo)].})],[u(fun`msg,%#,{[toolkit(org,orgname,%qo)] is not a masterorg.})])],0??,[u(fun`msg,%#,That player could not be found.)],?0?,[u(fun`msg,%#,What org did you want to transfer to?)],??1,[u(fun`msg,%#,{[name(%qp)] already belongs to [toolkit(org,orgname,%qo)]})])]
&FUN`UNKNOWN $Master Room: Org Commands=[setq(T,switch(%0,ships,1|ship,bases,2|base,planets,3|planet,1|ship))][setq(S,iter(sql({SELECT objectid FROM orgsys_spaceobjects WHERE objecttype='[after(%qT,|)]'}),before(%i0,:),,|))][setq(L,setdiff(squish(spaceobj(first(%qT,|)),|),%qS,|,,%b))][u(f_header,Ship Listing)]%r[align(20 14 4 10 30,ansi(hg,Name),ansi(hg,Class),,ansi(hg,Status),ansi(hg,Location))]%r[setq(L,iter(%qL,if(hasflag(%i0,SPACE-OBJECT),%i0)))][iter(%qL,u(fun`unknown`table,%i0),,%r)]%r%r[u(f_footer)]
&FUN`POSITION $Master Room: Org Commands=[sql(update orgsys_membership set position='[sqlescape(%3)]' where org='%2' and playerid='%1')][u(%!/fun`msg,%0,You set the [ansi(hw,[toolkit(org,orgname,%2)])] position for [name(%1)] to [ansi(hy,[toolkit(org,position,%1,toolkit(org,orgname,%2))])].)][u(%!/fun`msg,%1,[name(%0)] sets your position in [ansi(hw,[toolkit(org,orgname,%2)])] to [ansi(hy,[toolkit(org,position,%1,toolkit(org,orgname,%2))])].)]
&FUN`PROMOTE $Master Room: Org Commands=[setq(0,u(#5824/fun.org`ranknum,%0,%2),1,u(#5824/fun.org`ranknum,%1,%2))][if(cor(orflags(%0,Wrk),gt(sub(%q0,%q1),1)),[if(sql(select rank from orgsys_ranks where org='%2' and rank='[add(%q1,1)]'),[sql(update orgsys_membership set rank='[add(%q1,1)]' where org='%2' and playerid='%1')][u(%!/fun`msg,%0,You promote [name(%1)] to rank [u(#5824/fun.org`rank,%1,%2)].)][u(%!/fun`msg,%1,[name(%0)] promotes you to rank [u(#5824/fun.org`rank,%1,%2)].)],u(%!/fun`msg,%0,There is not a higher rank to promote to.))],u(%!/fun`msg,%0,You can't promote to that rank.))]
&FUN`SUBORGLIST $Master Room: Org Commands=%b%b[ansi(hw,[toolkit(org,orgname,%0)])]%R[iter([sql(select id from orgsys_orglist where parentorg=%0)],[repeat(%b,6)][toolkit(org,orgname,[itext(0)])][iter(sql(select id from orgsys_orglist where parentorg=[itext(0)]),%R[repeat(%b,10)][toolkit(org,orgname,[itext(0)])][iter(sql(select id from orgsys_orglist where parentorg=[itext(0)]),%R%b%b[repeat(%b,14)][toolkit(org,orgname,[itext(0)])][iter(sql(select id from orgsys_orglist where parentorg=[itext(0)]),%R%b%b[repeat(%b,18)][toolkit(org,orgname,[itext(0)])][iter(sql(select id from orgsys_orglist where parentorg=[itext(0)]),%R%b%b[repeat(%b,22)][toolkit(org,orgname,[itext(0)])],,%R)],,%R)],,%R)],,)],,%R)]
&LOCK.MANAGER $Master Room: Org Commands=if(or(orflags(%#,Wrk),iter(toolkit(org,parentchainbyname,sorgname(sorg(%0))),if(sql(SELECT COUNT(ID) FROM orgsys_managers WHERE orgid=%i0 AND playerid='[sqlescape(%:)]'),1,0))),1,0)
&LOCK.ADMIN $Master Room: Org Commands=if(orflags(%#,Wrk),1,0[u(fun`msg.unauthorized,%#)])
&LOCK.HELPER $Master Room: Org Commands=if(or(orflags(%#,Wrk),hasflag(%#,Helper)),1,0[u(fun`msg.unauthorized,%#)])
&LOCK.ROYAL $Master Room: Org Commands=if(orflags(%#,Wr),1,0[u(fun`msg.unauthorized,%#)])
&LOCK.WIZARD $Master Room: Org Commands=if(hasflag(%#,Wizard),1,0[u(fun`msg.unauthorized,%#)])
&VERSION $Master Room: Org Commands=OrgSYS [ansi(g,v3)].0